plugins {
    id 'java'

    id 'application'
    id 'org.flywaydb.flyway' version '9.19.0'
}

group = 'tech.qmates'
version = '1.0-SNAPSHOT'

application {
    mainClass = 'tech.qmates.openchat.web.Main'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.eclipse.jetty:jetty-server:11.0.15'
    implementation 'org.eclipse.jetty:jetty-servlet:11.0.15'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'
    implementation 'com.google.guava:guava:32.1.1-jre'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.assertj:assertj-core:3.24.2'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.4.0'
}

test {
    useJUnitPlatform()
    testLogging.events "passed", "skipped", "failed"
    outputs.upToDateWhen { false }
}

import org.flywaydb.gradle.task.FlywayMigrateTask

tasks.register('createProductionDb', FlywayMigrateTask) {
    url = "jdbc:sqlite:${project.projectDir}/src/main/resources/production.db"
}

tasks.register('createTestDb', FlywayMigrateTask) {
    url = "jdbc:sqlite:${project.projectDir}/src/test/resources/integration.test.db"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
